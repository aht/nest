#!/bin/sed -rf

# Transform a whole element closed at EOL of the same line as its start tag
s!<([^ </>!?]+)>([^>]+)</\1>\r?$!\\\1 \2!g

# Convert start tags without attributes
s!<([^ </>!?]+)>!\\\1<!g

# Convert self-closing elements with attributes that ends at EOL
s!<([^ </>!?]+)( +([^<>]+))* */>\r?$!\\\1[\3]!g

# Convert self-closing elements with attributes
s!<([^ </>!?]+)( +([^<>]+))* */>!\\\1[\3]<>!g

# Convert start tags with attributes
s!<([^ </>!?]+)( +([^<>]+))*>!\\\1[\3]<!g

# Convert end tags
s!</([^ </>!?]+)>!>!g

# Convert processing instructions that are closed at EOL
s!<\?([^ </>!]+)( +([^<>]+))*\?>\r?$!\\\?\1[\3]!g

# Convert processing instructions
s!<\?([^ </>!]+)( +([^<>]+))*\?>!\\\?\1[\3]<>!g


## For !DOCTYPE and comments, which fit inside a single tag and may span
## multiple lines, we just treat them as an element and let the rest of the
## tag be treated as its body

# Convert !DOCTYPEs
s:<!DOCTYPE *:\\!DOCTYPE<:g

# Convert comments
s:<!-- *:\\--<:g

# Convert comment endings
s:-->:>:g
